import org.springframework.boot.gradle.plugin.SpringBootPlugin

plugins {
    id 'java'
    id 'org.springframework.boot' version '3.2.12' apply false
    id 'maven-publish'
    id 'org.jetbrains.kotlin.jvm' version '2.1.0'
    id 'org.jetbrains.kotlin.plugin.spring' version '2.1.0'
    id 'jacoco'
    id 'io.github.gradle-nexus.publish-plugin' version '2.0.0'
    id 'com.palantir.git-version' version '3.1.0'
}
description = 'Library for standardising all HTTP related exception handling'
group = 'io.github.lsd-consulting'
version = gitVersion().replaceAll("^v", "")
logger.lifecycle("Build Version = ${version}")

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

subprojects {
    apply plugin: 'java-library'
    apply plugin: 'maven-publish'

    group = rootProject.getGroup()
    version = rootProject.getVersion()

    java {
        withSourcesJar()
        withJavadocJar()
    }
}

allprojects {
    repositories {
        mavenLocal()
        mavenCentral()
    }

    // Dependency Versioning
    apply plugin: 'io.spring.dependency-management'
    dependencyManagement {
        imports {
            mavenBom SpringBootPlugin.BOM_COORDINATES
            mavenBom 'org.springframework.cloud:spring-cloud-dependencies:2023.0.5'
            mavenBom 'org.jetbrains.kotlin:kotlin-bom:2.1.0'
        }
    }
}

apply from: 'publishing.gradle'
apply from: 'hooks.gradle'
